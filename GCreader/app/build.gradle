apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'

    defaultConfig {
        applicationId 'camp.pixel.challenge.gcreader'

        minSdkVersion 21
        targetSdkVersion 22

        versionCode getGitCommitNumber().toInteger()
        versionName '1.0'
    }

    return void
}

dependencies {
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.android.support:appcompat-v7:24.2.1'

    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'me.dm7.barcodescanner:zxing:1.9'
}

def getGitCommitNumber(referenceName = 'HEAD') {
    return executeShell("git rev-list --count ${referenceName}")
}

def executeShell(command) {
    def process = command.execute([], project.rootDir)

    process.waitFor()

    if (process.exitValue() != 0) {
        throw new RuntimeException(process.getErrorStream().text)
    }

    return process.text.trim()
}

